plugins {
    id 'java'
    id 'org.jetbrains.kotlin.multiplatform'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

kotlin {

    jvm {
        compilations.all {
            kotlinOptions {
                jvmTarget = "1.6"
                freeCompilerArgs += ["-Xuse-experimental=kotlin.Experimental"]
            }
        }
    }

    sourceSets {
        jvmMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
            }
        }
        jvmTest {
            dependencies {
                implementation project(':kotlintest-core')
                implementation project(":kotlintest-assertions")
                implementation project(':kotlintest-runner:kotlintest-runner-junit5')
                implementation project(':kotlintest-assertions:kotlintest-assertions-arrow')
                implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.2'
                implementation "com.nhaarman:mockito-kotlin:1.6.0"
                implementation 'org.mockito:mockito-core:2.24.0'
                // this is here to test that the intellij marker 'dummy' test doesn't appear in intellij
                api 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
                api 'org.junit.jupiter:junit-jupiter-api:5.4.0'
            }
        }
    }
}

kotlin.sourceSets.all {
    languageSettings {
        languageVersion = '1.3' // possible values: '1.0', '1.1', '1.2', '1.3'
        apiVersion = '1.3' // possible values: '1.0', '1.1', '1.2', '1.3'
        enableLanguageFeature('InlineClasses') // language feature name
    }
}

test {
    useJUnitPlatform()

    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    testLogging {
        events "PASSED", "FAILED", "SKIPPED", "STANDARD_OUT", "STANDARD_ERROR"
        exceptionFormat = 'full'
    }
}

publish.enabled = false